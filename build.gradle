plugins {
	id 'fabric-loom' version '1.10-SNAPSHOT'
	id "maven-publish"
	//FIGURE OUT HERE: https://github.com/modrinth/minotaur
	id "com.modrinth.minotaur" version "2.8.7"
}

/* Data Generation */
loom {
	accessWidenerPath = file("src/main/resources/jineric.accesswidener")
}

fabricApi {
	configureDataGeneration {
		client = true
	}
}

/* Dependencies */
repositories {
	mavenCentral()
	maven { 
		url "https://api.modrinth.com/maven" 
	}
	maven { 
		url = 'https://maven.minecraftforge.net/'
	}
	maven { 
		url "https://jitpack.io"
	}
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}
	maven {
		url = "https://maven.shedaniel.me/"
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

version = project.version
group = project.maven_group

base {
	archivesBaseName = project.archives_base_name
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "com.github.glitchfiend:TerraBlender-fabric:${project.minecraft_version}-${project.terrablender_version}"
	modApi "com.terraformersmc:modmenu:${project.mod_menu_version}"
	modApi "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"
	modApi "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
	include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:${project.mixin_extras_version}")))
	include(implementation ('com.github.Chocohead:Fabric-ASM:v2.3'))
}

/* Resource Processing */
processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

/* Publishing */
java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
	}
}
